CREATE OR ALTER PROCEDURE CRIA_BANCO_TESTE (@BANCOPESQUISAR VARCHAR(100), @NOMEFINAL VARCHAR(200))
AS
BEGIN

-- CHECA O NOME E O CAMINHO DOS BANCOS
DECLARE @IP VARCHAR(100);
DECLARE @BANCO VARCHAR(200);
SELECT @IP = S.IP, @BANCO = C.BDINITIALCATALOG 
FROM VMBANCO.DATASYSTEM.DBO.CLIENTE C 
	JOIN VMBANCO.DATASYSTEM.DBO.ESTRUTURACLIENTE E ON C.ESTRUTURAID = E.ID
	JOIN VMBANCO.DATASYSTEM.DBO.SERVIDORES S ON SERVIDORBD = S.SERVIDORID
WHERE C.NOME LIKE '%' + @BANCOPESQUISAR + '%'

PRINT 'ENCONTRADO O BD ' + @BANCO + ' NO IP ' + @IP

--BUSCA O ARQUIVO
DECLARE @CAMINHO VARCHAR(200) = '\\' + @IP + (CASE @IP WHEN '172.16.1.5' THEN '\BACKUP\BACKUP' ELSE '\BACKUP' END) + '\';
DECLARE @STRINGBUSCA VARCHAR(400) = 'DIR '+@CAMINHO+'\*'+@BANCO+'*.BAK /O:-D'
DECLARE @TBL_PESQUISA TABLE (STR VARCHAR(MAX))
INSERT @TBL_PESQUISA
EXEC XP_CMDSHELL @STRINGBUSCA
SELECT TOP 1 @CAMINHO += SUBSTRING(STR, 37, 4000)
FROM @TBL_PESQUISA WHERE STR LIKE '%.BAK'

PRINT 'ENCONTRADO O BACKUP ' + @CAMINHO 

--VERIFICA O NOME LOGICO DOS ARQUIVOS QUE SER√ÉO CRIADOS NO BD
DECLARE @COMANDOARQUIVOS NVARCHAR(MAX) = 'RESTORE FILELISTONLY FROM DISK = ''' + @CAMINHO + '''';

DECLARE @TBLARQUIVOS TABLE (LOGICALNAME VARCHAR(MAX), PHYSICALNAME VARCHAR(MAX), TYPE VARCHAR(MAX), FILEGROUPNAME VARCHAR(MAX), 
SIZE VARCHAR(MAX), MAXSIZE VARCHAR(MAX), FILEID VARCHAR(MAX), CREATELSN VARCHAR(MAX), DROPLSN VARCHAR(MAX), UNIQUEID VARCHAR(MAX), 
READONLYLSN VARCHAR(MAX), READWRITELSN VARCHAR(MAX), BACKUPSIZEINBYTES VARCHAR(MAX), SOURCEBLOCKSIZE VARCHAR(MAX), FILEGROUPID VARCHAR(MAX), 
LOGGROUPGUID VARCHAR(MAX), DIFFERENTIALBASELSN VARCHAR(MAX), DIFFERENTIALBASEGUID VARCHAR(MAX), ISREADONLY VARCHAR(MAX), ISPRESENT VARCHAR(MAX), 
TDETHUMBPRINT VARCHAR(MAX), SNAPSHOTURL VARCHAR(MAX))
INSERT @TBLARQUIVOS
EXEC SP_EXECUTESQL @COMANDOARQUIVOS

DECLARE @DATAFILE VARCHAR(200) = (SELECT LOGICALNAME FROM @TBLARQUIVOS WHERE TYPE = 'D') 
DECLARE @LOGFILE VARCHAR(200) = (SELECT LOGICALNAME FROM @TBLARQUIVOS WHERE TYPE = 'L')

-- MONTA O COMANDO DE RESTORE
DECLARE @COMANDORESTORE NVARCHAR (MAX) = 
CONCAT('RESTORE DATABASE ', @NOMEFINAL, ' FROM  DISK = ''' + @CAMINHO + ''' WITH  FILE = 1,  
MOVE N''', @DATAFILE, ''' TO N''D:\DATA\', @NOMEFINAL, '.MDF'', 
MOVE N''', @LOGFILE, ''' TO N''E:\DATA\', @NOMEFINAL, '_LOG.LDF'',  NOUNLOAD,  STATS = 10 ')

PRINT 'RESTAURANDO O BD ' + @NOMEFINAL

--RESTAURA
EXEC SP_EXECUTESQL @COMANDORESTORE
END
GO